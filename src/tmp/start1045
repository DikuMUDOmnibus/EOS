#! /bin/csh -f
# Written by Furey.
# With additions from Tony.
# With changes from Kahn.

# Set the port number.
set port = 1045
if ( "$1" != "" ) set port="$1"

# Change to area directory.
cd ../../../EOSBUILD/area

# Set limits.
nohup
nice +4
limit stack 1024k
if ( -e shutdown.txt ) rm -f shutdown.txt
unlimit core

while ( 1 )
    # If you want to have logs in a different directory,
    #   change the 'set logfile' line to reflect the directory name.
    set index = 1000
    while ( 1 )
	set logfile = ../../EOS/src/tmp/$index.log
	if ( ! -e $logfile ) break
	@ index++
    end

    # Run envy.
    if ( -e shutdown.txt ) rm -f shutdown.txt
    ../../EOS/src/tmp/envy $port >&! $logfile

    if ( -e core ) then
	    mv core ../../EOS/src/tmp/$index.core
    endif

    # Restart, giving old connections a chance to die.
    if ( -e shutdown.txt ) then
	rm -f shutdown.txt
	exit 0
    endif
    sleep 5
end
